
locals {
  # Define the absolute path for TLS directory.
  layer_tls_dir = abspath("${path.root}/tls")

  # Root domain for all services.
  root_domain = "iac.local"

  # TTL Policy for different environments
  ttl_policy = {
    "production"  = { max = 60 * 60 * 24 * 365, default = 60 * 60 * 24 * 30 } # Max 1 Year, Default 30 Days
    "staging"     = { max = 60 * 60 * 24 * 30, default = 60 * 60 * 24 * 7 }   # Max 30 Days, Default 7 Days
    "development" = { max = 60 * 60 * 24 * 7, default = 60 * 60 * 24 }        # Max 1 Day,   Default 1 Hour
    "default"     = { max = 60 * 60 * 24, default = 60 * 60 }                 # Fallback
  }

  # Dependency Roles corresponding to Service Catalog dependencies
  dependency_roles = merge([
    for sys_key, sys_config in var.service_catalog : {
      for dep_key, dep_config in sys_config.dependencies : "${sys_key}-${dep_config.service_name}" => {

        # Role Name. e.g. gitlab-postgres-role
        name = "${sys_key}-${dep_config.service_name}-role"

        # Domains. e.g. postgres.gitlab.iac.local AND gitlab.iac.local
        allowed_domains = [
          "${dep_config.service_name}.${sys_key}.${local.root_domain}",
          "${sys_key}.${local.root_domain}"
        ]

        # Metadata Injection: Write into certificate Subject OU. e.g. OU=production, OU=baremetal
        ou = [sys_config.stage, dep_config.runtime]

        max_ttl = lookup(local.ttl_policy, sys_config.stage, local.ttl_policy["default"]).max
        ttl     = lookup(local.ttl_policy, sys_config.stage, local.ttl_policy["default"]).default
      }
    }
  ]...)

  # Component Roles corresponding to Service Catalog components
  component_roles = merge([
    for sys_key, sys_config in var.service_catalog : {
      for comp_key, comp_config in sys_config.components : "${sys_key}-${comp_key}" => {

        # Role Name. e.g. harbor-frontend-role
        name = "${sys_key}-${comp_key}-role"

        # Domains. e.g. harbor.iac.local
        allowed_domains = [for s in comp_config.subdomains : "${s}.${local.root_domain}"]

        # Metadata Injection same as in dependency roles
        ou = [sys_config.stage, sys_config.runtime]

        max_ttl = lookup(local.ttl_policy, sys_config.stage, local.ttl_policy["default"]).max
        ttl     = lookup(local.ttl_policy, sys_config.stage, local.ttl_policy["default"]).default
      }
    }
  ]...)
}

locals {
  # Service Identity
  svc_name     = var.vault_compute.cluster_identity.service_name
  comp_name    = var.vault_compute.cluster_identity.component
  layer_number = var.vault_compute.cluster_identity.layer_number

  cluster_name = "${local.layer_number}-${local.svc_name}-${local.comp_name}"

  # Naming Convention
  nat_net_name      = "iac-${local.svc_name}-${local.comp_name}-nat"
  hostonly_net_name = "iac-${local.svc_name}-${local.comp_name}-hostonly"
  storage_pool_name = "iac-${local.svc_name}-${local.comp_name}"

  # Bridges
  svc_abbr             = substr(local.svc_name, 0, 3)
  comp_abbr            = substr(local.comp_name, 0, 3)
  nat_bridge_name      = "${local.svc_abbr}-${local.comp_abbr}-natbr"
  hostonly_bridge_name = "${local.svc_abbr}-${local.comp_abbr}-hostbr"
}
