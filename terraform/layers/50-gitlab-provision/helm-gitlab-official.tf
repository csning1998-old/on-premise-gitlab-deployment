
resource "helm_release" "gitlab" {

  depends_on = [
    kubernetes_secret.gitlab_tls,
    kubernetes_secret.gitlab_rails_secret,
    kubernetes_secret.gitlab_shell_secret,
    kubernetes_secret.gitlab_gitaly_secret,
    kubernetes_secret.gitlab_root_password,
    resource.kubernetes_secret.gitlab_s3_artifacts,
    resource.kubernetes_secret.gitlab_postgres_password,
    resource.kubernetes_secret.gitlab_redis_password
  ]

  name       = "gitlab"
  repository = "https://charts.gitlab.io/"
  chart      = "gitlab"
  version    = "9.8.2"

  namespace = kubernetes_namespace.gitlab.metadata[0].name
  timeout   = 900

  set = [
    {
      name  = "global.ingress.configureCertmanager"
      value = "false"
    },
    {
      name  = "installCertmanager"
      value = "false"
    }
  ]

  values = [
    yamlencode({
      global = {
        edition = "ce"
        hosts = {
          domain     = "iac.local"
          externalIP = data.terraform_remote_state.gitlab_cluster.outputs.gitlab_kubeadm_virtual_ip
        }

        certificates = {
          customCAs = [
            {
              secret = kubernetes_secret.gitlab_custom_ca.metadata[0].name
            }
          ]
        }

        # Target to Secret generated by Terraform
        ingress = {
          class = "nginx"
          tls = {
            enabled    = true
            secretName = kubernetes_secret.gitlab_tls.metadata[0].name
          }
          annotations = {
            "cert-manager.io/cluster-issuer" = ""
          }
        }

        # External Services
        psql = {
          password = {
            secret = "gitlab-postgres-password"
            key    = "password"
          }
          host     = data.terraform_remote_state.gitlab_postgres.outputs.gitlab_postgres_virtual_ip
          port     = data.terraform_remote_state.gitlab_postgres.outputs.gitlab_postgres_haproxy_rw_port
          username = "gitlab"
          database = "gitlabhq_production"
        }

        redis = {
          password = {
            secret = "gitlab-redis-password"
            key    = "password"
          }
          # host = "172.16.141.200" # TODO: Use variables to drive IP & Port
          host   = data.terraform_remote_state.gitlab_redis.outputs.gitlab_redis_virtual_ip
          port   = data.terraform_remote_state.gitlab_redis.outputs.gitlab_redis_haproxy_stats_port
          scheme = "rediss"
        }

        minio = { enabled = false }

        appConfig = {
          object_store = {
            enabled        = true
            proxy_download = true
            connection     = {}
          }
          artifacts = {
            bucket     = "gitlab-artifacts"
            connection = {}
          }

          lfs = {
            bucket     = "gitlab-lfs"
            connection = {}
          }

          packages = {
            bucket     = "gitlab-packages"
            connection = {}
          }

          uploads = {
            bucket     = "gitlab-uploads"
            connection = {}
          }

          backups = {
            bucket     = "gitlab-backups"
            tmpBucket  = "gitlab-backups-tmp"
            connection = {}
          }

          terraformState = {
            bucket     = "gitlab-terraform-state"
            connection = {}
          }
        }

        # Internal Secrets
        shell               = { authToken = { secret = kubernetes_secret.gitlab_shell_secret.metadata[0].name, key = "secret" } }
        gitaly              = { authToken = { secret = kubernetes_secret.gitlab_gitaly_secret.metadata[0].name, key = "token" } }
        rails               = { secret = { secret = kubernetes_secret.gitlab_rails_secret.metadata[0].name, key = "secret" } }
        initialRootPassword = { secret = kubernetes_secret.gitlab_root_password.metadata[0].name, key = "password" }
      }

      # Disable all built-in components
      nginx-ingress = { enabled = false }
      prometheus    = { install = false }
      postgresql    = { install = false }
      redis         = { install = false }
      registry      = { enabled = false }
      gitlab-runner = { install = false }

      gitlab = {
        webservice = {
          minReplicas = 2
          maxReplicas = 4
          kas         = { enabled = true }
        }
        sidekiq = {
          minReplicas = 2
          maxReplicas = 4
        }
      }
    })
  ]
}
